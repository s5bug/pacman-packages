pkgname=libgit2
pkgver=1.1.0
pkgrel=1
epoch=1
pkgdesc='A linkable library for Git'
arch=('any')
url='https://libgit2.org/'
license=('GPL2')
options=(!strip libtool staticlibs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        "3ds-no-mmap.patch")
makedepends=('cmake')
depends=('3ds-libssh2' '3ds-mbedtls' '3ds-zlib')
sha256sums=('41a6d5d740fd608674c7db8685685f45535323e73e784062cf000a633d420d1e'
            '398144de3967df923a8a77d143db89865114351914f27aab53c8de6676a2d0f5')
groups=('3ds-portlibs')

prepare() {
    cd "$pkgname-$pkgver"
    patch --forward --strip=1 --input="${srcdir}/3ds-no-mmap.patch"
}

build() {
  cd "$pkgname-$pkgver"

  source /opt/devkitpro/3dsvars.sh

  cmake -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/3ds.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DUSE_HTTPS="mbedTLS" \
    -DTHREADSAFE=OFF \
    -DBUILD_SHARED_LIBS=OFF \
    -DBUILD_CLAR=OFF \
    -G"Unix Makefiles" \
    .

  make
}

package() {
  cd "$pkgname-$pkgver"

  source /opt/devkitpro/3dsvars.sh

  make install DESTDIR="$pkgdir"
}
