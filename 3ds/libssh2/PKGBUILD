# Maintainer: Mary Strodl <ipadlover8322@gmail.com>
pkgname=3ds-libssh2
pkgver=1.9.0
pkgrel=1
pkgdesc='libssh2 is a client-side C library implementing the SSH2 protocol'
arch=('any')
url='https://www.libssh2.org/'
license=('BSD')
options=(!strip libtool staticlibs)
source=("${url}/download/libssh2-${pkgver}.tar.gz")
makedepends=('devkitpro-pkgbuild-helpers')
depends=('3ds-mbedtls')
sha256sums=('d5fb8bd563305fd1074dda90bd053fb2d29fc4bce048d182f96eaa466dfadafd')
groups=('3ds-portlibs')

build() {
  cd "libssh2-${pkgver}"

  source /opt/devkitpro/3dsvars.sh

  cmake -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/3ds.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DBUILD_TESTING=OFF \
    -G"Unix Makefiles" \
    .

  make
}

package() {
  cd "libssh2-${pkgver}"

  source /opt/devkitpro/3dsvars.sh

  make install DESTDIR="$pkgdir"
}
